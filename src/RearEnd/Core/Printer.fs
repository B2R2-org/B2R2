(*
  B2R2 - the Next-Generation Reversing Platform

  Copyright (c) SoftSec Lab. @ KAIST, since 2016

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.
*)

namespace B2R2.RearEnd

open System

/// Define a column of a table with a specified width in bytes (# of chars).
type TableColumn =
  | RightAligned of width: int
  | LeftAligned of width: int
with
  static member ofPaddedString isLast (s: string) = function
    | RightAligned w -> let s = s.PadLeft (w) in if isLast then s else s + " "
    | LeftAligned w -> if isLast then s else s.PadRight (w) + " "

/// Define a output configuration of a table.
type TableConfig = TableColumn list

module CS = ColoredSegment

module private PrinterConst =
  let [<Literal>] colWidth = 24

/// Our rear-end applications should *not* use System.Console or `printfn` to
/// directly output strings. Instead, they should resort to Printer to
/// "indirectly" print out strings to console.
type Printer =
  [<CompiledName "Print">]
  static member print s =
    OutString.toConsole s

  [<CompiledName "Print">]
  static member print s =
    ColoredString.toConsole s

  [<CompiledName "Print">]
  static member print (s: string, [<ParamArray>] args) =
    Console.Write (s, args)

  [<CompiledName "PrintLine">]
  static member println s =
    OutString.toConsoleLine s

  [<CompiledName "PrintLine">]
  static member println s =
    ColoredString.toConsoleLine s

  [<CompiledName "PrintLine">]
  static member println (s: string, [<ParamArray>] args) =
    Console.WriteLine (s, args)

  [<CompiledName "PrintLine">]
  static member println () =
    Console.WriteLine ()

  [<CompiledName "PrintRow">]
  static member printrow (indent, cfg: TableConfig, css: ColoredString list) =
    let lastIdx = List.length cfg - 1
    List.zip cfg css
    |> List.iteri (fun i (col, cs) ->
      if indent then Console.Write ("  ") else ()
      match cs with
      | (c, s) :: rest ->
        (c, TableColumn.ofPaddedString (i = lastIdx) s col) :: rest
        |> Printer.print
      | [] -> ())
    Console.WriteLine ()

  [<CompiledName "PrintRow">]
  static member printrow (indent, cfg: TableConfig, strs: string list) =
    let lastIdx = List.length cfg - 1
    List.zip cfg strs
    |> List.iteri (fun i (c, s) ->
      if indent then
        Console.Write ("  ")
      Console.Write (TableColumn.ofPaddedString (i = lastIdx) s c))
    Console.WriteLine ()

  [<CompiledName "PrintSectionTitle">]
  static member printSectionTitle title =
    [ CS.red "# "; CS.nocolor title ]
    |> Printer.println
    Printer.println ()

  [<CompiledName "PrintSubsectionTitle">]
  static member printSubsectionTitle (str: string) =
    Printer.println ("    - " + str)

  [<CompiledName "PrintSubsubsectionTitle">]
  static member printSubsubsectionTitle (str: string) =
    Printer.println ("         * " + str)

  [<CompiledName "PrintTwoCols">]
  static member printTwoCols (col1: string) (col2: string) =
    Printer.print (col1.PadLeft PrinterConst.colWidth + " ")
    Printer.println col2

  /// Print a two-column row while highlighting the second col.
  [<CompiledName "PrintTwoColsHi">]
  static member printTwoColsHi (col1: string) (col2: string) =
    Printer.print (col1.PadLeft PrinterConst.colWidth + " ")
    Printer.println [ CS.green col2 ]

  /// Print a two-column row where the second column is represented as a
  /// ColoredString.
  [<CompiledName "PrintTwoColsWithCS">]
  static member printTwoColsWithCS (col1: string) (col2: ColoredString) =
    Printer.print (col1.PadLeft PrinterConst.colWidth + " ")
    Printer.println col2

  [<CompiledName "PrintError">]
  static member printError str =
    [ CS.nocolor "[*] Error: "; CS.red str ] |> Printer.println
    Printer.println ()
